apply plugin: 'com.android.library'
apply plugin: 'maven-publish'
apply plugin: 'com.github.alexfu.androidautoversion'

android {
    compileSdkVersion 30
    buildToolsVersion "30.0.3"

    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 30

        versionCode androidAutoVersion.versionCode
        versionName androidAutoVersion.versionName + "." + (System.getenv("GITHUB_RUN_ID") ?: "0")

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    lintOptions {
        abortOnError false
    }
}

// Ensure that the publish task depends on assembly
tasks.all { task ->
    if (task instanceof AbstractPublishToMaven) {
        task.dependsOn assemble
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    androidTestImplementation('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    api 'androidx.appcompat:appcompat:1.2.0'
    api 'io.resourcepool:ssdp-client:2.4.3'
    api 'com.android.volley:volley:1.2.0'
    api 'org.slf4j:slf4j-nop:1.7.25'
    api 'com.stanfy:gson-xml-java:0.1.+'

    testImplementation'junit:junit:4.13.2'
}

def sweeprProperties = new Properties()
def sweeprPropertiesFile = rootProject.file("sweepr.properties")

if (sweeprPropertiesFile.exists()) {
    sweeprProperties.load(new FileInputStream(sweeprPropertiesFile))
} else {
    sweeprProperties.setProperty('github.username', System.getenv("GPR_USER") ?: "undefined-user")
    sweeprProperties.setProperty('github.token', System.getenv("GPR_API_KEY") ?: "undefined-token")
}

publishing {
    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/rosebud-iot/mobile-framework-android")
            credentials {
                username = sweeprProperties["github.username"]
                password = sweeprProperties["github.token"]
            }
        }
    }

    publications {
        upnpdiscovery(MavenPublication) {
            groupId 'com.sweepr'
            artifactId 'upnp-discovery'
            version androidAutoVersion.versionName

            artifact("$buildDir/outputs/aar/upnpdiscovery-release.aar")

            pom {
                name = 'NetworkTest'
                description = 'Discovery UPnP devices via SSDP protocol'
                url = 'https://github.com/rosebud-iot/speed-test-lib.git'

                licenses {
                    license {
                        name = 'MIT'
                    }
                }
            }

            // include dependencies
            pom.withXml {
                def dependencies = asNode().appendNode('dependencies')
                configurations.getByName("releaseCompileClasspath").getResolvedConfiguration().getFirstLevelModuleDependencies().each {
                    def dependency = dependencies.appendNode('dependency')
                    dependency.appendNode('groupId', it.moduleGroup)
                    dependency.appendNode('artifactId', it.moduleName)
                    dependency.appendNode('version', it.moduleVersion)
                }
            }
        }
    }
}

